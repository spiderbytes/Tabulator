XIncludeFile "Tabulator.sbi"

Enumeration
  #myWindow
  #myTabulatorGadget
EndEnumeration

Procedure TabulatorEvents(o)
  
  Select EventType()
    Case Tabulator::#EventType_RowClick
      Debug "#EventType_RowClick"
    Case Tabulator::#EventType_RowDblClick
      Debug "#EventType_RowDblClick"
    Case Tabulator::#EventType_RowContext
      Debug "#EventType_RowContext"
    Default
      Debug "EventType(): " + EventType()
  EndSelect
  
  Debug "---------------------------"
  
EndProcedure

Procedure SizeWindowEvent()
  
  ResizeGadget(#myTabulatorGadget, 10, 10, WindowWidth(#myWindow) - 20, WindowHeight(#myWindow) - 20)
  
EndProcedure

Procedure LineFormatter(cell, formatterParams)
  
  ! setTimeout(function() { // give cell enough time to be added to the DOM before calling sparkline formatter
  !   if (v_cell) {
  !     v_cell.getElement().sparkline(v_cell.getValue(), {width:"100%", type:"line", disableTooltips:false});
  !   }
  ! }, 10);
  
EndProcedure

Procedure Main()
  
  OpenWindow(#myWindow, 0, 0, 1000, 600, "TabulatorDemo", #PB_Window_ScreenCentered | #PB_Window_SizeGadget)
  
  BindEvent(#PB_Event_SizeWindow, @SizeWindowEvent(), #myWindow)
  
  ContainerGadget(#myTabulatorGadget, 10, 10, WindowWidth(#myWindow) - 20, WindowHeight(#myWindow) - 20) : CloseGadgetList()
  
  Protected Options
  
  ! v_options = {
  !   progressiveRender:true, //enable progressive rendering
  !   groupBy: 'gender',
  !   // selectable: true, // see: http://olifolkerd.github.io/tabulator/docs/#selectable
  !   layout:'fitColumns'
  ! }  
  
  Tabulator::BindGadget(#myTabulatorGadget, Options)
  
  Tabulator::BindTabulatorEvent(#myTabulatorGadget, @TabulatorEvents())
  
  Define *myColumn.Tabulator::sColumn
  
  NewList Columns.Tabulator::sColumn()
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title   = "Name"
  *myColumn\field   = "name"
  *myColumn\width   = 200
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title     = "Line Chart"
  *myColumn\field     = "line"
  *myColumn\width     = 160
  *myColumn\formatter = "@LineFormatter()"
  
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title     = "Progress"
  *myColumn\field     = "progress"
  *myColumn\width     = 100
  *myColumn\formatter = Tabulator::#Formatter_Progress
  *myColumn\sorter    = Tabulator::#Sorter_Number
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title = "Gender"
  *myColumn\field = "gender"
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title           = "Rating"
  *myColumn\field           = "rating"
  *myColumn\formatter       = Tabulator::#Formatter_Star
  *myColumn\formatterParams = ~"{\"stars\":\"5\"}";
  
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title     = "Favourite Color"
  *myColumn\field     = "col"
  *myColumn\width     = 50
  *myColumn\formatter = Tabulator::#Formatter_Color
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title     = "Date Of Birth"
  *myColumn\field     = "dob"
  
  *myColumn = Tabulator::InitColumn(AddElement(Columns()))
  *myColumn\title     = "Driver"
  *myColumn\field     = "car"
  *myColumn\width     = 50
  *myColumn\formatter = Tabulator::#Formatter_TickCross
  *myColumn\align     = Tabulator::#Align_Center
  
  Tabulator::SetAttribute2(#myTabulatorGadget, Tabulator::#TabulatorColumns, Columns())
  
  Protected SampleData
  
  ! v_sampledata = [
  !		{id:1,  name:"Oli Bob",            progress:12,  gender:"male",   rating:1, col:"red",    dob:"",           car:1,    line:[1, 20, 5, 3, 10, 13, 17, 15, 9, 11]},
  !		{id:2,  name:"Mary May",           progress:1,   gender:"female", rating:2, col:"blue",   dob:"14/05/1982", car:true, line:[10, 12, 14, 16, 13, 9, 7, 11, 10, 13]},
  !		{id:3,  name:"Christine Lobowski", progress:42,  gender:"female", rating:0, col:"green",  dob:"22/05/1982", car:"true"},
  !		{id:4,  name:"Brendon Philips",    progress:100, gender:"male",   rating:1, col:"orange", dob:"01/08/1980"},
  !		{id:5,  name:"Margret Marmajuke",  progress:16,  gender:"female", rating:5, col:"yellow", dob:"31/01/1999"},
  !		{id:6,  name:"Frank Harbours",     progress:38,  gender:"male",   rating:4, col:"red",    dob:"12/05/1966", car:1},
  !		{id:7,  name:"Jamie Newhart",      progress:23,  gender:"male",   rating:3, col:"green",  dob:"14/05/1985", car:true},
  !		{id:8,  name:"Gemma Jane",         progress:60,  gender:"female", rating:0, col:"red",    dob:"22/05/1982", car:"true"},
  !		{id:9,  name:"Emily Sykes",        progress:42,  gender:"female", rating:1, col:"maroon", dob:"11/11/1970"},
  !		{id:10, name:"James Newman",       progress:73,  gender:"male",   rating:5, col:"red",    dob:"22/03/1998"},
  !	];  
  
  Tabulator::SetAttribute(#myTabulatorGadget, Tabulator::#TabulatorData, SampleData)
  
EndProcedure

Tabulator::Init(@Main()) ; using standard-style (#Style_Tabulator)
